
import "pe" 

rule DuneQuixote_APT_initial_stage_dll {
	meta:		
		description = "DuneQuixote APT initial stage DLL version"
		classification = "Downloader/Dropper"
		type = "APT"
		Author = "Mohamed Talaat known on socials by Blu3Eye and DTM"
		date = "8-5-2024"
		version = "1"
	strings: 	
	// strings xor decryption function 
		$a1 = { 45 33 C0 48 8B D1 8A 42 ?? 41 FF C0}
	// Dynamic API loading technique
		$a2 = { 65 48 8B 01 48 8B 48 18 48 8B 51 20}
	// Domain generation algorithm 
		$a3 = { E8 ?? ?? ?? ?? 85 ?? 74 ?? B9 ?? ?? ?? ?? E8 ?? ?? ?? ?? 49 ?? ?? 4D ?? ?? 43 ?? ?? ?? 49 ?? ?? 83 ?? ?? 44 ?? ?? 44 ?? ?? ?? ?? 0A ?? ?? ?? 41 ?? ?? 44 ?? ?? 46 ?? ?? ?? 49 ?? ?? 49 ?? ?? ?? 72 ?? 44 ?? ?? ?? B8 ?? ?? ?? ?? }	
	condition: 
		((uint16(0) == 0x5a4d)) and ((pe.characteristics & pe.DLL)) and ((pe.number_of_exports == 2)) and ((all of ($a*))) 
}
